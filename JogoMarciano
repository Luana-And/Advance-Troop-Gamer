int arvoreMarciano;
int tentativas;
int limiteTentativas = 10;
String mensagem;
String input = "";
int melhorTentativa = Integer.MAX_VALUE;
boolean jogoAtivo = false;
boolean telaInicio = true;
boolean telaResultado = false;

void setup() {
    size(600, 500);
}

void draw() {
    background(153, 50, 204);
    textAlign(CENTER, CENTER);
    textSize(20);
    fill(0);

    if (telaInicio) {
        text("Jogo do Marciano", width / 2, 200);
        text("Pressione ENTER para começar", width / 2, 250);
    } else if (telaResultado) {
        text("Fim de Jogo!", width / 2, 150);
        text(mensagem, width / 2, 200);
        text("Melhor tentativa: " + (melhorTentativa == Integer.MAX_VALUE ? "-" : melhorTentativa), width / 2, 250);
        text("Pressione 'R' para jogar novamente", width / 2, 300);
    } else {
        text("Digite um número entre 1 e 100", width / 2, 150);
        text("Pressione Enter após digitar", width / 2, 180);
        text("Número digitado: " + input, width / 2, 240);
        text(mensagem, width / 2, 400);
    }
}

void keyPressed() {
    if (telaInicio && (key == ENTER || key == RETURN)) {
        telaInicio = false;
        iniciarJogo();
        return;
    }

    if (telaResultado && (key == 'r' || key == 'R')) {
        iniciarJogo();
        return;
    }

    if (key == ENTER || key == RETURN) {
        if (input.length() > 0 && jogoAtivo) {
            int tentativa = int(trim(input));
            tentativas++;

            if (tentativa == arvoreMarciano) {
                mensagem = "Parabéns! Você encontrou o Marciano!\nTotal de tentativas: " + tentativas;
                jogoAtivo = false;
                if (tentativas < melhorTentativa) {
                    melhorTentativa = tentativas;
                }
                telaResultado = true;
            } else if (tentativa < arvoreMarciano) {
                mensagem = "O Marciano está em uma árvore de número maior.";
            } else {
                mensagem = "O Marciano está em uma árvore de número menor.";
            }

            if (tentativas >= limiteTentativas && jogoAtivo) {
                mensagem = "Você atingiu o limite de tentativas! O Marciano estava na árvore " + arvoreMarciano;
                jogoAtivo = false;
                telaResultado = true;
            }
            input = "";
        }
    } else if (key >= '0' && key <= '9' && jogoAtivo) {
        input += key;
    } else if (key == BACKSPACE && input.length() > 0 && jogoAtivo) {
        input = input.substring(0, input.length() - 1);
    }
}

void iniciarJogo() {
    arvoreMarciano = int(random(1, 101));
    tentativas = 0;
    mensagem = "Adivinhe a árvore do Marciano! Você tem " + limiteTentativas + " tentativas.";
    input = "";
    jogoAtivo = true;
    telaResultado = false;
}
