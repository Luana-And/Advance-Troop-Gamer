int cols = 40;
int rows = 40;
int cellSize = 20;
int menuButtonX, menuButtonY,menuButtonW = 150, menuButtonH = 40;
int pontos = 0;


Snake snake;
PVector food;
boolean gameOver = false;

// Estados do jogo
boolean jogoIniciado = false;
boolean mostrarMenu = true;

// Texto da história
String historia = 
  "Há muito tempo, em uma floresta mágica,\n" +
  "vive uma jovem cobra chamada Luma.\n" +
  "Luma precisa comer muito para ganhar forças\n" +
  "e proteger a floresta das sombras que se aproximam.\n" +
  "Ajude Luma a crescer forte comendo o máximo que puder!";

// Dificuldade
int dificuldade = 1; // 0 = fácil, 1 = normal, 2 = difícil

// Botão de reinício
int buttonX, buttonY, buttonW = 150, buttonH = 40;

void settings() {
  size(cols * cellSize, rows * cellSize);
}

void setup() {
  frameRate(8);
}

void draw() {
  background(0);
  
  fill(255);
  textSize(16);
  textAlign(LEFT, TOP);
  text("Pontos: " + pontos, 10, 10);

  if (mostrarMenu) {
    mostrarTelaInicial();
    return;
  }

  if (!jogoIniciado) return;

  if (!gameOver) {
    snake.move();
    snake.checkCollision();
    snake.show();

    fill(255, 0, 0);
    rect(food.x * cellSize, food.y * cellSize, cellSize, cellSize);

    if (snake.head().equals(food)) {
      snake.grow();
      pontos += 10;
      spawnFood();
    }
  } else {
    mostrarGameOver();
  }
}

void mostrarTelaInicial() {
  fill(255);
  textAlign(CENTER);
  textSize(18);
  text(historia, width / 2, 80);

  // Botões de dificuldade
  textSize(20);
  button("FÁCIL", width / 2 - 75, 200, () -> iniciarComDificuldade(0));
  button("NORMAL", width / 2 - 75, 260, () -> iniciarComDificuldade(1));
  button("DIFÍCIL", width / 2 - 75, 320, () -> iniciarComDificuldade(2));
}

void button(String label, int x, int y, Runnable onClick) {
  int w = 150, h = 40;
  fill(50, 200, 50);
  rect(x, y, w, h, 10);

  fill(255);
  textSize(20);
  textAlign(CENTER, CENTER);
  text(label, x + w / 2, y + h / 2); // Centraliza o texto no botão

  if (mousePressed &&
      mouseX >= x && mouseX <= x + w &&
      mouseY >= y && mouseY <= y + h) {
    delay(200);
    onClick.run();
  }
}


void iniciarComDificuldade(int nivel) {
  dificuldade = nivel;
  frameRate(nivel == 0 ? 5 : nivel == 1 ? 8 : 12); // fácil, normal, difícil
  iniciarJogo();
  jogoIniciado = true;
  mostrarMenu = false;
}

void iniciarJogo() {
  snake = new Snake();
  spawnFood();
  gameOver = false;
  pontos = 0;
  loop();
}

void spawnFood() {
  food = new PVector(floor(random(cols)), floor(random(rows)));
  while (snake.contains(food)) {
    food = new PVector(floor(random(cols)), floor(random(rows)));
  }
}

void keyPressed() {
  if (!jogoIniciado || gameOver) return;

  if ((key == 'w' || key == 'W') && snake.ydir != 1) {
    snake.setDir(0, -1);
  } else if ((key == 's' || key == 'S') && snake.ydir != -1) {
    snake.setDir(0, 1);
  } else if ((key == 'a' || key == 'A') && snake.xdir != 1) {
    snake.setDir(-1, 0);
  } else if ((key == 'd' || key == 'D') && snake.xdir != -1) {
    snake.setDir(1, 0);
  }
}

void mousePressed() {
  if (gameOver) {
    if (mouseX >= buttonX && mouseX <= buttonX + buttonW &&
        mouseY >= buttonY && mouseY <= buttonY + buttonH) {
      iniciarJogo();
        } else if (mouseX >= menuButtonX && mouseX <= menuButtonX + menuButtonW && 
                   mouseY >= menuButtonY && mouseY <= menuButtonY + menuButtonH) {
         gameOver = false;
         jogoIniciado = false;
         mostrarMenu = true;
    }
  }
}

void mostrarGameOver() {
  fill(255, 0, 0);
  textSize(32);
  textAlign(CENTER, CENTER);
  text("Game Over!", width / 2, height / 2 - 60);

  // Desenhar botão de reiniciar
  buttonX = width / 2 - buttonW - 10;
  buttonY = height / 2;
  fill(50, 200, 50);
  rect(buttonX, buttonY, buttonW, buttonH, 10);

  fill(255);
  textSize(20);
  text("Reiniciar", buttonX + buttonW / 2, buttonY + buttonH / 2);
  
  
  // Botao menu
  menuButtonX = width / 2 + 10;
  menuButtonY = buttonY;
  fill(50, 150, 200);
  rect(menuButtonX, menuButtonY, menuButtonW, menuButtonH, 10);
  fill(255);
  text("Menu", menuButtonX + menuButtonW / 2, menuButtonY + menuButtonH / 2);
  
}

// Classe Snake
class Snake {
  ArrayList<PVector> body;
  int xdir = 1;
  int ydir = 0;

  Snake() {
    body = new ArrayList<PVector>();
    body.add(new PVector(floor(cols / 2), floor(rows / 2)));
    xdir = 1;
    ydir = 0;
  }

  void setDir(int x, int y) {
    if (body.size() > 1) {
      PVector head = body.get(0);
      PVector neck = body.get(1);
      if (head.x + x == neck.x && head.y + y == neck.y) return;
    }
    xdir = x;
    ydir = y;
  }

  void move() {
    if (xdir == 0 && ydir == 0) return;
    PVector head = body.get(0).copy();
    head.x += xdir;
    head.y += ydir;
    body.add(0, head);
    body.remove(body.size() - 1);
  }

  void grow() {
    PVector head = body.get(0).copy();
    head.x += xdir;
    head.y += ydir;
    body.add(0, head);
  }

  void show() {
    fill(0, 255, 0);
    for (PVector part : body) {
      rect(part.x * cellSize, part.y * cellSize, cellSize, cellSize);
    }
  }

  PVector head() {
    return body.get(0);
  }

  boolean contains(PVector pos) {
    for (PVector part : body) {
      if (part.equals(pos)) return true;
    }
    return false;
  }

  void checkCollision() {
    PVector head = body.get(0);
    if (head.x < 0 || head.x >= cols || head.y < 0 || head.y >= rows) {
      encerrarJogo();
    }

    for (int i = 1; i < body.size(); i++) {
      if (head.equals(body.get(i))) {
        encerrarJogo();
      }
    }
  }

  void encerrarJogo() {
    gameOver = true;
   
  }
}
